load("//build_rules:kernel_preamble.bzl", "make_metal_preamble_kernel_rules")
load("//build_rules:metal.bzl", "metal_library")

package(default_visibility = ["//visibility:public"])

config_setting(
    name = "ios_arm64",
    values = {
        "apple_platform_type": "ios",
        "cpu": "ios_arm64",
    },
)

config_setting(
    name = "ios_sim_arm64",
    values = {
        "apple_platform_type": "ios",
        "cpu": "ios_sim_arm64",
    },
)

# Generate preamble for common backend.
genrule(
    name = "generate_preamble_common",
    srcs = glob([
        "mlx/types/*",
        "mlx/backend/common/**/*",
    ]),
    outs = ["mlx/backend/common/compiled_preamble.cpp"],
    cmd = "bash $(location mlx/backend/common/make_compiled_preamble.sh) $@ clang . TRUE",
    tags = ["manual"],
    tools = ["mlx/backend/common/make_compiled_preamble.sh"],
)

cc_library(
    name = "preamble_common",
    srcs = [":generate_preamble_common"],
    # TODO: Unsure if this is necessary, consider removing.
    hdrs = [
        "mlx/backend/common/compiled_preamble.h",
    ],
    defines = ["MLX_METAL_VERSION=320"],
)

# Generate preamble for Metal backend.
make_metal_preamble_kernel_rules(kernel_specs = {
    "utils": {
        "src": "mlx/backend/metal/kernels/utils.h",
        "deps": [
            "mlx/backend/metal/kernels/bf16.h",
            "mlx/backend/metal/kernels/bf16_math.h",
            "mlx/backend/metal/kernels/complex.h",
            "mlx/backend/metal/kernels/defines.h",
        ],
    },
    "unary_ops": {
        "src": "mlx/backend/metal/kernels/unary_ops.h",
        "deps": [
            "mlx/backend/metal/kernels/erf.h",
            "mlx/backend/metal/kernels/expm1f.h",
        ],
    },
    "binary_ops": {
        "src": "mlx/backend/metal/kernels/binary_ops.h",
    },
    "ternary_ops": {
        "src": "mlx/backend/metal/kernels/ternary_ops.h",
    },
    "reduce_utils": {
        "src": "mlx/backend/metal/kernels/reduce_utils.h",
        "deps": [
            "mlx/backend/metal/kernels/atomic.h",
            "mlx/backend/metal/kernels/reduction/ops.h",
        ],
    },
    "scatter": {
        "src": "mlx/backend/metal/kernels/scatter.h",
        "deps": [
            "mlx/backend/metal/kernels/indexing.h",
        ],
    },
    "gather": {
        "src": "mlx/backend/metal/kernels/gather.h",
        "deps": [
            "mlx/backend/metal/kernels/indexing.h",
        ],
    },
    "hadamard": {
        "src": "mlx/backend/metal/kernels/hadamard.h",
    },
    # TODO: Parameterize like CMake
    "arange": {"src": "mlx/backend/metal/kernels/arange.h"},
    "copy": {"src": "mlx/backend/metal/kernels/copy.h"},
    "unary": {"src": "mlx/backend/metal/kernels/unary.h"},
    "binary": {"src": "mlx/backend/metal/kernels/binary.h"},
    "binary_two": {"src": "mlx/backend/metal/kernels/binary_two.h"},
    "fft": {
        "src": "mlx/backend/metal/kernels/fft.h",
        "deps": [
            "mlx/backend/metal/kernels/fft/radix.h",
            "mlx/backend/metal/kernels/fft/readwrite.h",
            "mlx/backend/metal/kernels/steel/defines.h",
        ],
    },
    "ternary": {"src": "mlx/backend/metal/kernels/ternary.h"},
    "softmax": {"src": "mlx/backend/metal/kernels/softmax.h"},
    "scan": {"src": "mlx/backend/metal/kernels/scan.h"},
    "sort": {"src": "mlx/backend/metal/kernels/sort.h"},
    "reduce": {
        "src": "mlx/backend/metal/kernels/reduce.h",
        "deps": [
            "mlx/backend/metal/kernels/reduction/reduce_all.h",
            "mlx/backend/metal/kernels/reduction/reduce_col.h",
            "mlx/backend/metal/kernels/reduction/reduce_row.h",
            "mlx/backend/metal/kernels/reduction/reduce_init.h",
        ],
    },
    "steel/gemm/gemm": {
        "src": "mlx/backend/metal/kernels/steel/gemm/gemm.h",
        "deps": [
            "mlx/backend/metal/kernels/steel/defines.h",
            "mlx/backend/metal/kernels/steel/utils.h",
            "mlx/backend/metal/kernels/steel/utils/type_traits.h",
            "mlx/backend/metal/kernels/steel/utils/integral_constant.h",
            "mlx/backend/metal/kernels/steel/gemm/loader.h",
            "mlx/backend/metal/kernels/steel/gemm/params.h",
            "mlx/backend/metal/kernels/steel/gemm/transforms.h",
            "mlx/backend/metal/kernels/steel/gemm/mma.h",
        ],
    },
    "steel/gemm/kernels/steel_gemm_fused": {"src": "mlx/backend/metal/kernels/steel/gemm/kernels/steel_gemm_fused.h"},
    "steel/gemm/kernels/steel_gemm_masked": {
        "src": "mlx/backend/metal/kernels/steel/gemm/kernels/steel_gemm_masked.h",
        "deps": [
            "mlx/backend/metal/kernels/steel/defines.h",
        ],
    },
    "steel/gemm/kernels/steel_gemm_splitk": {"src": "mlx/backend/metal/kernels/steel/gemm/kernels/steel_gemm_splitk.h"},
    "steel/conv/conv": {
        "src": "mlx/backend/metal/kernels/steel/conv/conv.h",
        "deps": [
            "mlx/backend/metal/kernels/steel/utils.h",
            "mlx/backend/metal/kernels/steel/utils/type_traits.h",
            "mlx/backend/metal/kernels/steel/utils/integral_constant.h",
            "mlx/backend/metal/kernels/steel/defines.h",
            "mlx/backend/metal/kernels/steel/gemm/mma.h",
            "mlx/backend/metal/kernels/steel/gemm/transforms.h",
            "mlx/backend/metal/kernels/steel/conv/params.h",
            "mlx/backend/metal/kernels/steel/conv/loader.h",
            "mlx/backend/metal/kernels/steel/conv/loaders/loader_channel_l.h",
            "mlx/backend/metal/kernels/steel/conv/loaders/loader_channel_n.h",
        ],
    },
    "steel/conv/kernels/steel_conv": {"src": "mlx/backend/metal/kernels/steel/conv/kernels/steel_conv.h"},
    "steel/conv/kernels/steel_conv_general": {
        "src": "mlx/backend/metal/kernels/steel/conv/kernels/steel_conv_general.h",
        "deps": [
            "mlx/backend/metal/kernels/steel/defines.h",
            "mlx/backend/metal/kernels/steel/conv/loaders/loader_general.h",
        ],
    },
    "quantized": {"src": "mlx/backend/metal/kernels/quantized.h"},
    "gemv_masked": {"src": "mlx/backend/metal/kernels/gemv_masked.h"},
})

cc_library(
    name = "mlx",
    srcs = glob(
        ["mlx/**/*.cpp"],
        exclude = [
            "mlx/backend/metal/kernels/**/*",
            "mlx/backend/no_cpu/**/*",
            "mlx/backend/no_metal/**/*",
            "mlx/distributed/mpi/**/*",
            "mlx/io/no_safetensors.cpp",
            "mlx/io/no_gguf.cpp",
            "mlx/backend/metal/nojit_kernels.cpp",
            "mlx/backend/common/compiled_cpu.cpp",
            "mlx/backend/common/compiled_nocpu.cpp",
        ],
    ) + select({
        ":ios_arm64": [
            "mlx/backend/common/compiled_nocpu.cpp",
        ],
        ":ios_sim_arm64": [
            "mlx/backend/common/compiled_nocpu.cpp",
        ],
        "//conditions:default": ["mlx/backend/common/compiled_cpu.cpp"],
    }),
    hdrs = glob(
        ["mlx/**/*.h"],
        exclude = [
            # "mlx/backend/metal/kernels/**/*",
            "mlx/backend/no_cpu/**/*",
            "mlx/backend/no_metal/**/*",
            "mlx/distributed/mpi/**/*",
        ],
    ),
    data = [":mlx_metallib_file"],
    defines = [
        "MLX_METAL_VERSION=320",
        "ACCELERATE_NEW_LAPACK",
        "STRINGIFY_(x)=#x",
        "STRINGIFY(x)=STRINGIFY_(x)",
    ] + select({
        ":ios_arm64": ["METAL_PATH=STRINGIFY($(location //:mlx_metallib_file))"],
        ":ios_sim_arm64": ["METAL_PATH=STRINGIFY($(location //:mlx_metallib_file))"],
        "//conditions:default": ["METAL_PATH=STRINGIFY($(rootpath //:mlx_metallib_file))"],
    }),
    linkopts = [
        "-framework",
        "Metal",
        "-framework",
        "Foundation",
    ],
    deps = [
        ":preamble_common",
        ":preamble_metal",
        "@fmt",
        "@gguf_tools",
        "@metal_cpp",
        "@nlohmann_json//:singleheader-json",
    ],
)

cc_test(
    name = "tests",
    srcs = glob(["tests/*.cpp"]),
    deps = [
        ":mlx",
        "@doctest//doctest",
        "@doctest//doctest:main",
    ],
)

metal_library(
    name = "kernels",
    srcs = glob(["mlx/backend/metal/kernels/**/*.metal"]),
    hdrs = glob(["mlx/backend/metal/kernels/**/*.h"]),
)

# To finalize the metallib to be named as mlx.metallib
genrule(
    name = "mlx_metallib_genrule",
    srcs = [":kernels.metallib"],
    outs = ["mlx.metallib"],
    cmd = "cp $(location :kernels.metallib) $@",
)

filegroup(
    name = "mlx_metallib_file",
    srcs = [":mlx_metallib_genrule"],
)
